cmake_minimum_required(VERSION 3.1)
project(networkReliability)
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake")
set(Boost_USE_STATIC_LIBS	OFF)
set(Boost_USE_MULTITHREADED	ON)
set(Boost_USE_STATIC_RUNTIME	OFF)
option(USE_GRAPHVIZ "Include graphViz support" ON)
if(CMAKE_COMPILER_IS_GNUCC)
	add_definitions("-Wall")
endif()
find_package(Boost 1.53.0 COMPONENTS graph program_options system serialization filesystem regex timer chrono REQUIRED)
find_package(truncatedBinomialDistribution REQUIRED CONFIG)
find_package(Qt5 COMPONENTS Widgets Core Gui)
find_package(maxFlowAlgorithms REQUIRED CONFIG)
find_package(sampling REQUIRED CONFIG)
find_package(OpenMP REQUIRED)
if(USE_GRAPHVIZ)
	find_package(GraphViz)
endif()
find_package(aliasMethod REQUIRED)
if(WIN32)
	find_package(mpir)
	find_package(mpfr)
else()
	#mpfr needs to be listed as an imported target so that we can use target_link_librarie later on. If we don't have this then it just adds a -lmpfr which probably picks up the wrong thing. 
	find_library(mpfr_LIBRARY mpfr)
	add_library(mpfr SHARED IMPORTED)
	set_target_properties(mpfr PROPERTIES IMPORTED_LOCATION ${mpfr_LIBRARY})

	find_package(BISON)
	find_package(FLEX)
	add_subdirectory(ExhaustiveFunctionSearch)
	add_subdirectory(ExhaustiveFunctionEvaluate)
	add_subdirectory(empiricalFunction)
endif()
add_subdirectory(copyText)
add_subdirectory(networkReliabilityCommon)
add_subdirectory(methods)
add_subdirectory(generateGrid)
find_package(R REQUIRED)
#Attempt to generate the data
add_custom_target(BuildRData ALL ${CMAKE_COMMAND} -DR_COMMAND=${R_COMMAND} -P ${CMAKE_CURRENT_SOURCE_DIR}/RPackage/buildRData.cmake WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/RPackage)
add_subdirectory(RPackage)
add_subdirectory(observationVisualiser)
add_subdirectory(subObservationVisualiser)
add_subdirectory(turnip)
if(WIN32)
	install(CODE "execute_process(COMMAND \"${R_COMMAND}\" CMD INSTALL --no-multiarch RPackage/\$\{CMAKE_INSTALL_CONFIG_NAME\} WORKING_DIRECTORY ${CMAKE_BINARY_DIR})")
else()
	install(CODE "execute_process(COMMAND \"${R_COMMAND}\" CMD INSTALL RPackage/ WORKING_DIRECTORY ${CMAKE_BINARY_DIR})")
endif()
